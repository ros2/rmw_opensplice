set "_BUILDTIME_OSPL_HOME=@OpenSplice_HOME@/"

:: Give priority to the OSPL_HOME environment variable because the one detected at build time may
:: refer to an invalid path, e.g. for users using binaries.

:: Warn about changing the OpenSplice location
if "%OSPL_HOME%" NEQ "" (
  if "%OSPL_HOME%" NEQ "%_BUILDTIME_OSPL_HOME%" (
    echo [opensplice_cmake_module] OSPL_HOME environment variable is set to "%OSPL_HOME%" which is different to what it was when the workspace was built: "%_BUILDTIME_OSPL_HOME%" 1>&2
  )
)

if "%OSPL_HOME%" == "" (
  echo [opensplice_cmake_module] OSPL_HOME environment variable not set. Using OSPL_HOME that was detected when the workspace was built: "%_BUILDTIME_OSPL_HOME%"
  set "OSPL_HOME=%_BUILDTIME_OSPL_HOME%"
)

if not exist %OSPL_HOME% (
  echo [opensplice_cmake_module] Error: OSPL_HOME environment variable does not point to a valid directory: "%OSPL_HOME%" 1>&2
  set "_BUILDTIME_OSPL_HOME="
  goto :eof
)

call "%OSPL_HOME%\release.bat" 1> nul

set "OSPL_URI=file://%AMENT_CURRENT_PREFIX%/share/opensplice_cmake_module/config/ros_ospl.xml"
set "OSPL_INFOFILE=<stdout>"
set "OSPL_ERRORFILE=<stderr>"
set OSPL_VERBOSITY=2

:: If ROS_DOMAIN_ID is not set, set it to the default of 0.
:: This is required because rmw_opensplice_cpp cannot work unless it is set.
if "%ROS_DOMAIN_ID%"=="" set ROS_DOMAIN_ID=0
set "_BUILDTIME_OSPL_HOME="
